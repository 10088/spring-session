buildscript {
	ext {
		releaseBuild = version.endsWith('RELEASE')
		snapshotBuild = version.endsWith('SNAPSHOT')
		milestoneBuild = !(releaseBuild || snapshotBuild)

		springBootVersion = '2.2.0.M6'
	}

	repositories {
		gradlePluginPortal()
		maven { url 'https://repo.spring.io/plugins-release/' }
	}

	dependencies {
		classpath 'io.spring.gradle:spring-build-conventions:0.0.26.RELEASE'
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
	}
}

apply plugin: 'io.spring.convention.root'

group = 'org.springframework.session'
description = 'Spring Session'

subprojects {
	apply plugin: 'io.spring.javaformat'

	configurations.all {
		resolutionStrategy {
			cacheChangingModulesFor 0, "seconds"
			cacheDynamicVersionsFor 0, "seconds"
		}
	}

	plugins.withType(JavaPlugin) {
		sourceCompatibility = JavaVersion.VERSION_1_8
	}

	plugins.withId('io.spring.dependency-management') {
		dependencyManagement {
			resolutionStrategy {
				cacheChangingModulesFor 0, "seconds"
			}
		}
	}

	tasks.withType(Test) {
		useJUnitPlatform()
	}
}
